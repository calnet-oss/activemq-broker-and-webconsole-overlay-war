// https://github.com/scalding/gradle-waroverlay-plugin
buildscript {
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath "org.scaldingspoon.gradle:gradle-waroverlay-plugin:0.9.3"
    }
}

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'waroverlay'
apply plugin: 'maven-publish'

// ActiveMQ version
version projectVersion
group projectGroup

sourceCompatibility = 1.8
targetCompatibility = 1.8

allprojects {
    repositories {
        mavenLocal()
        // If using a Maven proxy, put the property settings in
        // ~/.gradle/gradle.properties for default_proxy_url,
        // default_proxy_username and default_proxy_password.
        if (project.hasProperty("default_proxy_url")) {
            maven {
                url project.property("default_proxy_url")
                credentials {
                    username project.property("default_proxy_username")
                    password project.property("default_proxy_password")
                }
            }
        }
        jcenter()
        mavenCentral()
    }
}

dependencies {
    runtime(group: 'org.apache.activemq', name: 'activemq-web-console', version: projectVersion) {
        transitive = true
    }
}

war {
    exclude "WEB-INF/activemq.xml"
}

// publishing with './gradlew publish'
publish.dependsOn assemble
publishToMavenLocal.dependsOn assemble
publishing {
    publications {
        maven(MavenPublication) {
            artifactId project.name
            version projectVersion
            groupId projectGroup
            from components.web
        }
    }

    repositories {
        maven {
            // Put the property settings in ~/.gradle/gradle.properties for
            // default_deploy_url, default_deploy_lib_release_repo
            // default_deploy_username and default_deploy_password.
            url "${project.property('default_deploy_url')}/${default_deploy_lib_release_repo}"
            credentials {
                username project.property("default_deploy_user")
                password project.property("default_deploy_password")
            }
        }
    }
}
