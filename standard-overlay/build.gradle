plugins {
    id 'java'
    id 'war'
    id 'maven-publish'
}

apply plugin: 'io.freefair.war-overlay'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

dependencies {
    runtimeOnly group: 'org.apache.activemq', name: 'activemq-jdbc-store', version: projectVersion

    // https://mvnrepository.com/artifact/org.apache.derby/derby
    // note that derby 10.17+ requires Java 21
    def derbyVersion = '10.16.1.1'
    runtimeOnly group: 'org.apache.derby', name: 'derby', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyclient', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbynet', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbytools', version: derbyVersion
    // https://mvnrepository.com/artifact/org.apache.commons/commons-dbcp2
    runtimeOnly(group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.12.0') {
        exclude module: 'commons-logging'
    }

    // I don't know why, but Derby doesn't work without all (or, some, at
    // least) of the Locale dependencies.
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_cs', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_de_DE', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_es', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_fr', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_hu', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_it', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_ja_JP', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_ko_KR', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_pl', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_pt_BR', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_ru', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_zh_CN', version: derbyVersion
    runtimeOnly group: 'org.apache.derby', name: 'derbyLocale_zh_TW', version: derbyVersion

    // https://mvnrepository.com/artifact/jakarta.xml.bind/jakarta.xml.bind-api
    runtimeOnly 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.2'

    // https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
    runtimeOnly 'ch.qos.logback:logback-classic:1.5.7'

    // https://mvnrepository.com/artifact/org.slf4j/slf4j-api
    runtimeOnly 'org.slf4j:slf4j-api:2.0.16'
}

war {
    // https://docs.freefair.io/gradle-plugins/current/reference/#_io_freefair_war_overlay
    overlays {
        activemq {
            from "org.apache.activemq:activemq-web-console:$projectVersion@war"
            excludes = [
                "WEB-INF/activemq.xml",
                "WEB-INF/lib/commons-logging*.jar",
                // don't introduce Jetty dependencies via Jetty websocket JARs
                "WEB-INF/lib/websocket-*.jar"
            ]
        }
    }
}

// publishing with './gradlew publish'
publish.dependsOn assemble
publishToMavenLocal.dependsOn assemble
publishing {
    publications {
        maven(MavenPublication) {
            artifactId parent.name
            version projectVersion
            groupId projectGroup
            from components.web
        }
    }

    repositories {
        maven {
            // Put the property settings in ~/.gradle/gradle.properties for
            // default_deploy_url, calnet_deploy_lib_repo,
            // default_deploy_user and default_deploy_password.
            url "${project.property('default_deploy_url')}/${calnet_deploy_lib_repo}"
            credentials {
                username project.property("default_deploy_user")
                password project.property("default_deploy_password")
            }
        }
    }
}
